use Test;
use Test::Deeply::Relaxed;
use ProcessedPod;

# A helper class for RakuClosureTemplates
multi sub gen-closure-template (Str $tag) is export {
    my $start = '<' ~ $tag ~ '>';
    my $end = '</' ~ $tag ~ '>';
    return sub (%prm, %tml? --> Str) {
        $start ~ (%prm<contents> // '') ~ $end;
    }
}
plan 8;

my $processor = ProcessedPod.new;
my $pv = 0;
my $rv;

my @templates = <block-code comment declarator defn dlist-end dlist-start escaped footnotes format-b format-c
        format-i format-k format-l format-n format-p format-r format-t format-u format-x glossary heading
        item list meta unknown-name output para pod raw source-wrap table toc >;

my %templates  = @templates Z=> @templates.map( { gen-closure-template( $_ ) });
%templates<escaped> = sub ($s) { $s };
%templates<meta> = sub ( %prm, %tmp ) {
    "<meta>\n"
    ~ %prm<meta>.map( { $_<name> ~ '=' ~ $_<value> ~ "\n"} )
    ~ '</meta>'
};
%templates<source-wrap> = sub (%prm, %tml ) { "<file><body>{ %prm<body> }</body>{ %prm<metadata> }</file>"};
%templates<heading> = sub (%prm, %tml) {
    "<config>" ~ %prm<text> ~ %prm<config>.sort.fmt("%s=>%s") ~ '</config>'
};
$processor.templates(%templates);

=begin pod  :kind("Language") :subkind("Language") :category("fundamental") :content-columns

=TITLE testing

=SUBTITLE more tests

Stuff

=head1 first

=end pod

$rv = $processor.render-block( $=pod[$pv++] );
like $rv, /
    'category=>fundamental'
    .+ 'content-columns=>True'
    .+ 'kind=>Language'
    .+ 'subkind=>Language'
/, 'got configs';

is-deeply-relaxed $processor.pod-file.pod-config-data, %( :kind<Language>, :subkind<Language>, :category<fundamental>, :content-columns ), 'got pod config data';

=begin pod  :different<This is different> :difficult<shouldnt be>

=TITLE testing again

=SUBTITLE more tests

Stuff and Nonsense

=config head1 :temporary

=head1 second

=end pod
$rv = $processor.render-block( $=pod[$pv]) ;
unlike $rv, /
    'difficult=>shouldnt be'
/, 'second pod does not change the configs';
like $rv, /
    "head1=>temporary\tTrue"
    /, 'but config directive does affect the configs';

isnt-deeply-relaxed $processor.pod-file.pod-config-data, %( :different<This is different>, :difficult<shouldnt be> ),
        'only first pod config data included';

$processor.emit-and-renew-processed-state;

$rv = $processor.render-block( $=pod[$pv]) ;
is-deeply-relaxed
        $processor.pod-file.pod-config-data,
        %( :different("This is different"), :difficult("shouldnt be") ),
        'second block config data now file config data because of reset';

=begin pod
=AUTHOR A.N. Writer
=SUMMARY Some summarised remarks

Stuff
=config head1 :this<is one> :that<is another>
=head1 third

=end pod

$processor.emit-and-renew-processed-state;
$rv = $processor.render-tree( $=pod[++$pv] );
like $rv, /
    "head1=>that\tis another"
    \s* "this\tis one"
/, 'got multiple attributes in config directive';

like $processor.source-wrap,
        /
        '<file><body>'
        .+ '</body>'
        '<meta>'
        [ \s* 'Author=A.N. Writer' | \s* 'Summary=Some summarised remarks' ] **2
        \s* '</meta></file>'
        /, 'Got meta data structure';
done-testing;
