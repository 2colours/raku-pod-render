use Test;
use Test::Deeply::Relaxed;
use ProcessedPod;

# A helper class for RakuClosureTemplates
multi sub gen-closure-template (Str $tag) is export {
    my $start = '<' ~ $tag ~ '>';
    my $end = '</' ~ $tag ~ '>';
    return sub (%prm, %tml? --> Str) {
        $start ~ (%prm<contents> // '') ~ $end;
    }
}
plan 4;

my $processor = ProcessedPod.new;
my $pv = 0;

my @templates = <block-code comment declarator defn dlist-end dlist-start escaped footnotes format-b format-c
        format-i format-k format-l format-n format-p format-r format-t format-u format-x glossary heading
        item list meta unknown-name output para pod raw source-wrap table toc >;

my %templates  = @templates Z=> @templates.map( { gen-closure-template( $_ ) });
%templates<escaped> = sub ($s) { $s };
%templates<meta> = sub ( %prm, %tmp ) {
    "<meta>\n"
    ~ %prm<meta>.map( { $_<name> ~ '=' ~ $_<value> ~ "\n"} )
    ~ '</meta>'
};
%templates<source-wrap> = sub (%prm, %tml ) { "<file><body>{ %prm<body> }</body>{ %prm<metadata> }</file>"};

$processor.templates(%templates);

=begin pod  :kind("Language") :subkind("Language") :category("fundamental")

=TITLE testing

=SUBTITLE more tests

Stuff

=end pod

$processor.render-block( $=pod[$pv++] );

is-deeply-relaxed $processor.pod-file.pod-config-data, %( :kind<Language>, :subkind<Language>, :category<fundamental>), 'got pod config data';

=begin pod  :different<This is different> :difficult<shouldnt be>

=TITLE testing again

=SUBTITLE more tests

Stuff and Nonsense

=end pod

$processor.render-block( $=pod[$pv]) ;
isnt-deeply-relaxed $processor.pod-file.pod-config-data, %( :different<This is different>, :difficult<shouldnt be> ), 'only first config data allowed';

$processor.emit-and-renew-processed-state;

$processor.render-block( $=pod[$pv]) ;
is-deeply-relaxed $processor.pod-file.pod-config-data, %( :different("This is different"), :difficult("shouldnt be") ), 'second block config data accepted';

=begin pod
=AUTHOR A.N. Writer
=SUMMARY Some summarised remarks

Stuff

=end pod

$processor.emit-and-renew-processed-state;
$processor.render-tree( $=pod[++$pv] );
like $processor.source-wrap,
        /
        '<file><body>'
        .+ 'Stuff'
        .+ '</body>'
        '<meta>'
        [ \s* 'Author=A.N. Writer' | \s* 'Summary=Some summarised remarks' ] **2
        \s* '</meta></file>'
        /, 'Got meta data structure';
done-testing;
